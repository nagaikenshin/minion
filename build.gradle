plugins {
	id 'java-library'
	id 'maven'
	id 'signing'
}

dependencies {
	testImplementation 'org.apache.commons:commons-lang3:3.6'
	testImplementation 'junit:junit:4.12'
}

repositories {
	mavenLocal()
	mavenCentral()
}

group = 'org.kyojo'
version = '0.8.0'
sourceCompatibility = 1.6
targetCompatibility = 1.6
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

if(JavaVersion.current().isJava8Compatible()) {
	allprojects {
		tasks.withType(Javadoc) {
			options.addStringOption('Xdoclint:none', '-quiet')
		}
	}
}

javadoc {
	options.locale = 'en_US'
}

task javadocGen(type: Javadoc) {
	source = sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadocGen.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment {
				MavenDeployment deployment -> signing.signPom(deployment)
			}

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom.project {
				name 'minion'
				packaging 'jar'
				// optionally artifactId can be defined here
				description 'Object data exchange helper'
				url 'https://github.com/nagaikenshin/minion'

				scm {
					connection 'scm:git:git://github.com/nagaikenshin/minion.git'
					developerConnection 'scm:git:ssh://github.com:nagaikenshin/minion.git'
					url 'https://github.com/nagaikenshin/minion/tree/master'
				}

				licenses {
					license {
						name 'The Apache License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id 'nagaikenshin'
						name 'nagaikenshin'
						email 'nagai@nagaikenshin.com'
					}
				}
			}
		}
	}
}
